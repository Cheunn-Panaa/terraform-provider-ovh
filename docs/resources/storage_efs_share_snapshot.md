---
subcategory: "Enterprise File Storage"
---

# ovh_storage_efs_share_snapshot (Resource)

Creates a snapshot for an EFS share.

Please note the `time_sleep` resource that was added as the `ovh_storage_efs_share` resource cannot be destroyed when any `manual`[^1] type `ovh_storage_efs_share_snapshot` resources are still attached to it.

[^1]: Snapshot resources created using Terraform `ovh_storage_efs_share_snapshot` resource will always have `manual` type.

## Example usage

```terraform
data "ovh_storage_efs" "efs" {
  service_name = "XXX"
}

# This resource will destroy (at least) 10 seconds after ovh_storage_efs_share_snapshot resource
resource "ovh_storage_efs_share" "share" {
  service_name = data.ovh_storage_efs.efs.service_name
  name         = "share"
  description  = "My share"
  protocol     = "NFS"
  size         = 100
}

resource "ovh_storage_efs_share_snapshot" "snapshot" {
  depends_on = [time_sleep.wait_10_seconds]

  service_name = data.ovh_storage_efs.efs.service_name
  share_id     = ovh_storage_efs_share.share.id
  name         = "snapshot"
  description  = "My snapshot"
}

# This resource adds a delay between ovh_storage_efs_share_snapshot and ovh_storage_efs_share resources destruction
resource "time_sleep" "wait_10_seconds" {
  depends_on = [ovh_storage_efs_share.share]

  destroy_duration = "10s"
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `service_name` (String) Service name
- `share_id` (String) Share ID

### Optional

- `description` (String) Snapshot description
- `name` (String) Snapshot name

### Read-Only

- `created_at` (String) Snapshot creation date
- `id` (String) Snapshot ID
- `path` (String) Snapshot path
- `status` (String) Snapshot status
- `type` (String) Snapshot type
